import("//llvm/utils/gn/build/toolchain/compiler.gni")

supported_toolchains = []
# FIXME: On macOS, stage2_unix currently doesn't copy libc++ headers to
# the out dir, but clang relies on them on mac to compile code that uses C++
# standard library headers. scudo needs C++ standard library headers, so disable
# this on mac until stage2_unix correctly copies libc++ headers.
if (target_os != "win" && target_os != "mac")  {
  supported_toolchains += [ "//llvm/utils/gn/build/toolchain:stage2_unix" ]
}
if (android_ndk_path != "") {
  supported_toolchains += [
    "//llvm/utils/gn/build/toolchain:stage2_android_aarch64",
    "//llvm/utils/gn/build/toolchain:stage2_android_arm",
  ]
}

group("scudo") {
  deps = []
  foreach(toolchain, supported_toolchains) {
    deps += [
      "standalone/tests:ScudoCUnitTest($toolchain)",
      "standalone/tests:ScudoCxxUnitTest($toolchain)",
      "standalone/tests:ScudoUnitTest($toolchain)",
    ]
  }
  testonly = true
}

# This target is unused, it only exists to satisfy
# sync_source_lists_from_cmake.py.
source_set("sources") {
  sources = [
    "scudo_allocator.cpp",
    "scudo_allocator.h",
    "scudo_allocator_combined.h",
    "scudo_allocator_secondary.h",
    "scudo_crc32.cpp",
    "scudo_crc32.h",
    "scudo_errors.cpp",
    "scudo_errors.h",
    "scudo_flags.cpp",
    "scudo_flags.h",
    "scudo_interface_internal.h",
    "scudo_malloc.cpp",
    "scudo_platform.h",
    "scudo_termination.cpp",
    "scudo_tsd.h",
    "scudo_tsd_exclusive.cpp",
    "scudo_tsd_shared.cpp",
  ]
}
